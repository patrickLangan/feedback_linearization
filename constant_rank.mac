/*
 * Check if the rank of a matrix M(x) is constant for all x.
 *
 * This function is intended to be used to check if a distribution has constant
 * rank.  From a distribution span{f₁(x), ..., fₙ(x)} create a matrix whose
 * columns are fᵢ, 1 ≤ i ≤ n.
 *
 * Method:
 * 1. Remove columns of M(x) which are linearly dependent for all x.
 *    Note that the resultant matrix M'(x) will likely have some remaining
 *    columns which are linearly dependent for some specific x points.
 * 2. Show that the only solution to c₁M'.₁(x) + ... + cₙM'.ₙ(x) = 0 is
 *    c₁ = ... = cₙ = 0.  This proves that the matrix columns are linearly
 *    independent for all x, thus M'(x) is constant full rank and M(x) has a
 *    constant rank equal to rank(M'(x)).
 */
constant_rank(M) := block(
	[
		i: 1,
		n: length(M[1]),
		R, Mc, M_tmp
	],
	Mc: super_radcan(M),
	R: rank(Mc),

	while R # n do (
		M_tmp: submatrix(Mc, i),
		if rank(M_tmp) = R then (
			Mc: copymatrix(M_tmp),
			n: n - 1
		) else (
			i: i + 1
		)
	),

	C: genmatrix(c, n, 1),
	eq: list_matrix_entries(Mc . C),
	sol: solve(eq, listofvars(eq)),

	/* Note that c₁ = ... = cₙ = 0 is guaranteed to be one of the solutions */
	if length(sol) # 1 then
		return(false),

	return(R)
)$

